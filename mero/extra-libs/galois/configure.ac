AC_PREREQ([2.63])
AC_INIT([galois], [0.1], [mero@xyratex.com])

# refer to the "7.4 Nesting Packages" section of autoconf manual for
# explanation on why this directive is used
AC_CONFIG_AUX_DIR([.])

AM_INIT_AUTOMAKE([-Wall -Werror nostdinc foreign subdir-objects silent-rules])
AM_SILENT_RULES([yes])

LT_INIT([disable-static])

AC_CONFIG_SRCDIR([src/galois.c])
AC_CONFIG_MACRO_DIR([m4])

AC_CONFIG_HEADERS([config.h])

# PACKAGE_VERSION is a default autoconf variable, generated by AC_INIT from
# it's "version" argument
AC_SUBST([LT_RELEASE], [$PACKAGE_VERSION])

# the following variables are only for internal use within this configure script
# external scripts should use collection of variables, which is automatically
# provided by autoconf:
#
#    @srcdir@
#    @top_srcdir@
#    @abs_top_srcdir@
#    @builddir@
#    @top_builddir@
#    @abs_top_builddir@
#
SRCDIR="$srcdir"
ABS_SRCDIR="$(cd $srcdir; pwd)"
ABS_BUILDDIR="$(pwd)"

AC_SUBST([GALOIS_ABS_SRCDIR], [$ABS_SRCDIR])


#
# Configuration options
#

AC_ARG_ENABLE([tests],
        [AS_HELP_STRING([--enable-tests], [enable tests])],
        [], [enable_tests=no]
)
AM_CONDITIONAL([ENABLE_TESTS],
               [test "x$enable_tests" = xyes])

AC_ARG_WITH([linux],
        [AS_HELP_STRING([--with-linux=path],
                        [set path to Linux kernel dir, default is /lib/modules/`uname -r`/build])],
        [LINUX=$with_linux; LINUX_OBJ=$LINUX; LINUX_MOD=$LINUX],
        [LINUX=/lib/modules/`uname -r`; LINUX_OBJ=$LINUX/build; LINUX_MOD=$LINUX]
)
AC_SUBST([LINUX_OBJ])
AC_SUBST([LINUX_MOD])

#
# Checking for required programs
#

AC_PROG_CC

#
# Checking for required libraries
#

#
# Checking for required header files
#

AC_CHECK_HEADERS([stdlib.h string.h sys/time.h])

#
# Checking for required types
#

#
# Checking for compiler characteristics
#

#
# Checking for required library functions
#

AC_FUNC_MALLOC
AC_CHECK_FUNCS([gettimeofday])

#
# Checking for required linux kernel API
#

#
# Handle configuration options
#

#
# Setup output files
#

AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([src/linux_kernel/Kbuild])

AC_OUTPUT
